plugins {
    alias(libs.plugins.android.application)
    id 'com.google.gms.google-services'
}

def getEnvVariable(String key) {
    def props = new Properties()
    def envFile = rootProject.file(".env")
    if (envFile.exists()) {
        props.load(new FileInputStream(envFile))
    }
    return props.getProperty(key) ?: ""
}

android {
    namespace 'com.mihirniyogi.inspiringquotesexample'
    compileSdk 35

    signingConfigs {
        config {
            def storePath = System.getenv("KEYSTORE_PATH")
            if (storePath != null) {
                storeFile file(storePath)
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    }

    defaultConfig {
        applicationId "com.mihirniyogi.inspiringquotesexample"
        minSdk 24
        targetSdk 35
        versionCode 4
        versionName "1.0.3"

        // incredibly unsafe - for demo purpose only - do NOT do this in a production environment
        buildConfigField "String", "QUOTE_API_KEY", "\"${getEnvVariable('QUOTE_API_KEY')}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    implementation libs.okhttp
    implementation platform(libs.firebase.bom)
    implementation(libs.firebase.appdistribution.api)
    implementation(libs.firebase.appdistribution)

    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}